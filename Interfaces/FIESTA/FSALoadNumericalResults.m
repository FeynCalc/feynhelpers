(* ++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++ *)

(* :Title: FSALoadNumericalResults									*)

(*
	This software is covered by the GNU General Public License 3.
	Copyright (C) 2021-2024 Vladyslav Shtabovenko
*)

(* :Summary: 	Generates input for LoopIntegralFromPropagators				*)

(* ------------------------------------------------------------------------ *)

FSALoadNumericalResults::usage=
"FSALoadNumericalResults[files] is a simple function that loads numerical
results generated by the FIESTA script created via FSAPrepareSDEvaluate   into
Mathematica. The argument files is the output of FSAPrepareSDEvaluate that
contains the full paths to FIESTAScript.m and the output file.

The optionChop (set to 10^(-10) by default) tells the function to remove
numerical artefacts.";

FSALoadNumericalResults::failmsg =
"Error! FSALoadNumericalResults has encountered a fatal problem and must abort the computation. \
The problem reads: `1`"

Begin["`Package`"]
End[]

Begin["`FSALoadNumericalResults`Private`"]

fslnrVerbose::usage="";

Options[FSALoadNumericalResults] = {
	Abort		-> True,
	Chop		-> 10^(-10),
	FCVerbose	-> False
};

FSALoadNumericalResults[files:{__String}.., opt:OptionsPattern[]] :=
	FSALoadNumericalResults[{#, "dummy"}, opt]&/@files;

FSALoadNumericalResults[files:{{_String, _String}..}, opt:OptionsPattern[]] :=
	FSALoadNumericalResults[#, opt]&/@files;

FSALoadNumericalResults[{script_String, resFileName_String}, OptionsPattern[]] :=
	Block[{ dir, res, optChop, optN, resFile},

		optChop								= OptionValue[Chop];


		If[	OptionValue[FCVerbose]===False,
			fslnrVerbose=$VeryVerbose,
			If[	MatchQ[OptionValue[FCVerbose], _Integer],
				fslnrVerbose=OptionValue[FCVerbose]
			];
		];

		resFile = FileNameJoin[{DirectoryName[script],resFileName}];

		If[	!FileExistsQ[resFile],
			If[	OptionValue[Abort],
				Message[FSALoadNumericalResults::failmsg,"The file " <> resFile <> "does not exist."];
				Abort[],
				FCPrint[0,"FSALoadNumericalResults: The file ",resFile,"does not exist.", FCDoControl->fslnrVerbose];
				Return[FCGV["NoResult"]]
			]
		];

		res = Get[resFile];

		If[optChop=!=False,
			res = Chop[res,optChop]
		];

		Return[res];

	];

End[]
